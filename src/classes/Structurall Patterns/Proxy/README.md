# OOP-Patterns: Proxy
There are many uses for the proxy design pattern. Five of the more common types of proxy are:
 
 + `Cache Proxy`. A cache proxy improves the performance of the underlying object's members when they perform long-running tasks that return seldom-changing results. For example, the underlying object may provide a method that calculates prime numbers. When the first call to the proxy's matching method is made, the call is passed to the real object. The results from the call are stored within the proxy object and returned to the client. For subsequent calls, the cached information in the proxy can be returned without recalculating the prime numbers.

 + `Protection Proxy`. A protection proxy adds a layer of security to the underlying subject object. For example, the real object may access a database and retrieve sensitive data. The protection proxy could add methods or properties that allow the client object to provide appropriate authentication before allowing the data to be returned. It could also filter the data according to the rights of the authenticated user.
   
 + `Remote Proxy`. A remote proxy provides a local object that references a subject object in another location, generally via a network connection. The proxy performs the required actions to encode the requests for network transfer and accept the results from the remote resource before returning them to the client. A common example of a remote proxy is the local class generated by Visual Studio to provide access to a web service.
 
 + `Smart Proxy`. Smart proxies add extra functionality to the calls to the real object's members. This functionality is often invisible to the client object. For example, counting the existing references to a resource-intensive object so that when the counter reaches zero the object's data can be removed from memory. You could also use a smart proxy to log calls to the underlying object's members.

 + `Virtual Proxy`. A virtual proxy provides a simplified version of a complex object. Only when the detail of the object is required is the main object actually populated, providing a form of lazy initialisation. For example, a file management utility such as Windows Explorer may use an object for each file that is visible on the screen. When obtaining the file list, the file name, size and other easy-to-retrieve information would be held in proxy objects. Only when the "document preview" command is requested would the real object be created and populated with the full contents of the file, as these are slower to access and require more memory.


### This is a `Virtual Proxy` realization

Some service Interface

```apex
public interface ImageI {
    void show();
}
```

The realization of service. It makes an immediate loading some very heavy Object

```apex
public with sharing class SpecificImage implements ImageI {
    private String path;

    public SpecificImage(String path) {
        this.path = path;
        // an immediate loading some very heavy Object
        this.load();
    }

    private void load() {
        System.debug('Loading Image on path: ' + this.path);
    }

    public void show() {
        System.debug('Showing Image on path: ' + this.path);
    }
}
```

The proxy that can delay of initialization an instance of the object that makes an immediate load of some heavy data

```apex
public with sharing class ImageProxy implements ImageI {
    private String path;
    private SpecificImage image;

    public ImageProxy(String path) {
        this.path = path;
    }

    public void show() {
        if (this.image == null) this.image = new SpecificImage(path);
        this.image.show();
    }
}
```

### Example:

```apex

public with sharing class ProxyExample {

    public void run() {
        String path = 'D:\\Images\\myImg.png';

        // Without proxy image is loading immediately
        // even if the user doesn't want to show it
        SpecificImage img = new SpecificImage(path);
        // img.show();


        // in this case if the user won't call the show() method
        // the image is not loading
        ImageProxy imageProxy = new ImageProxy(path);
        imageProxy.show();
    }
}
```

Result:

```text
        // Without proxy image is loading immediately
        // even if the user doesn't want to show it
        Loading Image on path: D:\Images\myImg.png
        
        // in this case if the user won't call the show() method
        // the image is not loading
        Loading Image on path: D:\Images\myImg.png
        Showing Image on path: D:\Images\myImg.png
```
